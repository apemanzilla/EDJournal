plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id "com.jfrog.bintray" version "1.7.3"
}

group = 'me.apemanzilla'
version = '1.1.3'

sourceCompatibility = 1.8
targetCompatibility = 1.8

eclipse.project {
	natures 'org.eclipse.buildship.core.gradleprojectnature'
	natures 'org.springsource.ide.eclipse.gradle.core.nature'
}

tasks.withType(JavaCompile) {
	options.fork = true
	options.incremental = true
}

repositories { mavenCentral() }

dependencies {
	compileOnly 'org.projectlombok:lombok:1.16.18'

	implementation 'com.google.code.gson:gson:2.8.1'
	implementation 'com.google.guava:guava:23.0'
	implementation 'org.slf4j:slf4j-api:1.7.25'

	testImplementation 'junit:junit:4.12'
	testImplementation 'org.slf4j:slf4j-simple:1.7.25'
}

task sourceJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
	
	group = 'build'
	description = 'Assembles a jar archive containing the main sources.'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	from javadoc.destinationDir
	classifier = 'javadoc'
	
	group = 'build'
	description = 'Assembles a jar archive containing the javadocs.'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourceJar
			artifact javadocJar
		}
	}
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	
	pkg {
		repo = 'maven'
		name = 'edjournal'
		publications = ['mavenJava']
		publish = true
		
		version {
			name = project.version
			released = new Date()
		}
	}
}